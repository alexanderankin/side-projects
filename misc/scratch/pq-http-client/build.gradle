import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'spring-conventions'
    id 'picocli-conventions'
}

// for shadow, use the client CLI
application.mainClass = 'side.scratch.pq.http.client.Http'

// for gradle task, run the demo
tasks.named('run', JavaExec) {
    mainClass = 'side.scratch.pq.http.client.PqHttpClientScratch'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework:spring-core'

    // // https://mvnrepository.com/artifact/io.projectreactor/reactor-bom
    // implementation platform('io.projectreactor:reactor-bom:2024.0.7')
    implementation 'io.projectreactor.netty:reactor-netty'

    // // https://mvnrepository.com/artifact/io.netty/netty-tcnative // openssl, dynamic
    // // https://mvnrepository.com/artifact/io.netty/netty-tcnative-boringssl-static
    // implementation 'io.netty:netty-tcnative-boringssl-static:2.0.72.Final'
    // implementation 'io.netty:netty-tcnative-boringssl-static:2.0.70.Final'
    switch (family()) {
        case Os.FAMILY_MAC ->
            implementation 'io.netty:netty-tcnative-boringssl-static::osx-aarch_64'
        case Os.FAMILY_WINDOWS ->
            implementation 'io.netty:netty-tcnative-boringssl-static::windows-x86_64'
        case Os.FAMILY_UNIX ->
            implementation 'io.netty:netty-tcnative-boringssl-static::linux-x86_64'
    }

    // conventions
    implementation 'org.bouncycastle:bcpkix-jdk18on'

    implementation project(':lib:picocli-lib')

    // waiting for spring boot 3.6.x?
    // https://github.com/spring-projects/spring-boot/commit/8077a0dfc3b0fc7a3470df05b7f3373903d7acad
    // implementation 'io.netty:netty-pkitesting'
}

static String family() {
    if (Os.isFamily(Os.FAMILY_MAC)) {
        return Os.FAMILY_MAC
    } else if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return Os.FAMILY_WINDOWS
    } else if (Os.isFamily(Os.FAMILY_UNIX)) {
        return Os.FAMILY_UNIX
    }
    throw new UnsupportedOperationException();
}
