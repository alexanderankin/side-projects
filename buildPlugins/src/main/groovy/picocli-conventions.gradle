import java.nio.file.Files
import java.nio.file.StandardOpenOption
import java.nio.file.attribute.PosixFilePermissions

plugins {
    id 'spring-conventions'
    id 'application'
    id 'com.gradleup.shadow'
}

dependencies {
    implementation 'info.picocli:picocli:4.7.7'
    annotationProcessor 'info.picocli:picocli-codegen:4.7.7'
    constraints {
        implementation 'info.picocli:picocli-spring-boot-starter:4.7.7'
    }

    implementation 'ch.qos.logback:logback-classic'
}

def projectName = project.name

tasks.register('shadowJarExecutable') {
    def inputFile = tasks.named('shadowJar').get().outputs.files.singleFile.toPath()
    inputs.file inputFile

    def outputFile = inputFile.parent.resolve(projectName)
    outputs.file outputFile

    doLast {
        try (var os = Files.newOutputStream(outputFile, StandardOpenOption.CREATE)) {
            os.write('#!/usr/bin/env -S java -jar\n'.getBytes())
            os.write(Files.readAllBytes(inputFile))
            Files.setPosixFilePermissions(outputFile, PosixFilePermissions.fromString("rwxr-xr-x"))
        }
    }
    dependsOn 'shadowJar'
}
