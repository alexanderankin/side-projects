plugins {
    id 'spring-conventions'
    id 'org.springframework.boot' version '3.2.1'
    id 'com.google.cloud.tools.jib' version '3.4.0'
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // implementation 'org.apache.httpcomponents.client5:httpclient5'
    implementation 'commons-io:commons-io:2.15.1'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    itestImplementation 'org.testcontainers:testcontainers'
    itestImplementation 'org.apache.commons:commons-collections4:4.4'
}

configurations {
    la {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    la project(path: ':cloud:util:logging-proxy:logged-application', configuration: 'la')
}

tasks.named('integrationTest', Test) {
    inputs.files(configurations.la)
    systemProperty('loggedAppJar', configurations.la.singleFile.path)
}

jib {
    from {
        image = 'eclipse-temurin:21-jre-alpine@sha256:9a21ac97b76e52f4b58d5d6c7fdd459cd600cce8724a31fc0a2b4346b35bced2'
    }
    to {
        image = 'daveankin/logging-proxy:netty'
    }
    container.entrypoint = [
            'sh',
            '-c',
            'exec java $JAVA_OPTS -cp @/app/jib-classpath-file org.cloud.logging.proxy.sb.netty.AsyncLoggingProxyApp'
    ]
}
